locals {
  upwind_access_token = try(jsondecode(data.http.upwind_get_access_token_request[0].response_body).access_token, null)
}

data "http" "upwind_get_access_token_request" {
  count = var.upwind_skip_http ? 0 : 1

  method = "POST"
  url = format(
    "%s/oauth/token",
    var.upwind_region == "us" ? var.upwind_auth_endpoint :
    var.upwind_region == "eu" ? replace(var.upwind_auth_endpoint, ".upwind.", ".eu.upwind.") :
    var.upwind_region == "me" ? replace(var.upwind_auth_endpoint, ".upwind.", ".me.upwind.") :
    var.upwind_region == "pdc01" ? replace(var.upwind_auth_endpoint, ".upwind.",
    ".pdc01.upwind.") :
    var.upwind_auth_endpoint
  )

  request_headers = {
    "Content-Type" = "application/x-www-form-urlencoded"
  }

  request_body = join("&", [
    "grant_type=client_credentials",
    format(
      "audience=%s",
      var.upwind_region == "us" ? var.upwind_integration_endpoint :
      var.upwind_region == "eu" ? replace(var.upwind_integration_endpoint, ".upwind.", ".eu.upwind.") :
      var.upwind_region == "me" ? replace(var.upwind_integration_endpoint, ".upwind.", ".me.upwind.") :
      var.upwind_region == "pdc01" ? replace(var.upwind_integration_endpoint,
      ".upwind.", ".pdc01.upwind.") :
      var.upwind_integration_endpoint
    ),
    "client_id=${var.upwind_client_id}",
    "client_secret=${var.upwind_client_secret}"
  ])

  retry {
    attempts = 3
  }

  lifecycle {
    precondition {
      condition     = var.upwind_client_id != null && var.upwind_client_secret != null
      error_message = "Invalid client credentials. Please verify your client ID and client secret."
    }
  }
}
